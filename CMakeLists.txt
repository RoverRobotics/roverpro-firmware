cmake_minimum_required(VERSION 3.12)
project(bootypic VERSION 1.9.3 LANGUAGES C)

file(GLOB HEADERS ${PROJECT_SOURCE_DIR}/**.h)
STRING(TOLOWER "pic${CMAKE_SYSTEM_PROCESSOR}" MCU_NAME)
STRING(TOLOWER "p${CMAKE_SYSTEM_PROCESSOR}" MCU_NAME_2)

set(DEVICE_SRC "${CMAKE_CURRENT_SOURCE_DIR}/devices/${MCU_NAME}")
set(FIRMWARE_SRC  "${CMAKE_CURRENT_SOURCE_DIR}/../Power_Board/src")

add_executable(bootypic
  bootloader.c
  "${DEVICE_SRC}/boot_user.c"
  "${FIRMWARE_SRC}/power.c"
  "${FIRMWARE_SRC}/battery.c"
  "${FIRMWARE_SRC}/stdfunctions.c"
  "${FIRMWARE_SRC}/i2clib.c"
  )

target_include_directories(bootypic
  PRIVATE
  "${DEVICE_SRC}"
  "${FIRMWARE_SRC}"
  )

target_compile_options(bootypic PRIVATE -O1 -ffunction-sections)
target_compile_definitions(bootypic PRIVATE BOOTYPIC)

set(APP_LINKER_SCRIPT "${DEVICE_SRC}/${MCU_NAME_2}_app.gld")
set(BOOT_LINKER_SCRIPT "${DEVICE_SRC}/${MCU_NAME_2}_boot.gld")

target_link_options(bootypic
  PRIVATE "LINKER:--script=${BOOT_LINKER_SCRIPT},-Map=${CMAKE_CURRENT_BINARY_DIR}/bootypic.map,--no-isr"
  )

add_custom_command(
  TARGET bootypic POST_BUILD
  #todo: do we need -omf?
  COMMAND "${XC16_bin2hex_EXECUTABLE}" -omf=${CMAKE_EXECUTABLE_FORMAT} "$<TARGET_FILE:bootypic>"
  BYPRODUCTS bootypic.hex
)
